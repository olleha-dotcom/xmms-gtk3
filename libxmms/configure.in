dnl Process this file with autoconf to produce a configure script.

AC_INIT([configfile.c])

AC_CONFIG_HEADERS([config.h])

dnl libtool version:
LIBXMMS_MAJOR_VERSION=4
LIBXMMS_MINOR_VERSION=1
LIBXMMS_MICRO_VERSION=3
LIBXMMS_VERSION=$LIBXMMS_MAJOR_VERSION.$LIBXMMS_MINOR_VERSION.$LIBXMMS_MICRO_VERSION

AM_INIT_AUTOMAKE([libxmms], [$LIBXMMS_VERSION])

AC_SUBST([LIBXMMS_MAJOR_VERSION])
AC_SUBST([LIBXMMS_MINOR_VERSION])
AC_SUBST([LIBXMMS_MICRO_VERSION])

AC_PREFIX_PROGRAM([xmms])
dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

LT_INIT([shared static])

AC_C_BIGENDIAN

XMMS_FUNC_POSIX([sched_setscheduler nanosleep])

PKG_PROG_PKG_CONFIG
PKG_CHECK_MODULES([GLIB], [glib-2.0 >= 2.56 gthread-2.0],
		  [],
		  [AC_MSG_ERROR([*** GLib >= 2.56 with gthread-2.0 is required ***])])
PKG_CHECK_MODULES([GTK], [gtk+-3.0 >= 3.24],
		  [],
		  [AC_MSG_ERROR([*** GTK+ >= 3.24 not installed - please install first ***])])

if test -n "$GCC"
then
  CFLAGS="$CFLAGS -Wall -Wpointer-arith"
fi

AC_HEADER_TIME

AC_CHECK_HEADERS([sched.h])
AC_CHECK_HEADERS([sys/sched.h])
AC_CHECK_HEADERS([unistd.h])
AC_CHECK_HEADERS([sys/time.h])

AC_CHECK_HEADERS([iconv.h])
AC_CHECK_FUNCS([iconv])

AC_CACHE_CHECK([for nl_langinfo(CODESET)], [xmms_cv_func_langinfo], [
AC_TRY_COMPILE([#include <langinfo.h>],
        [char *codeset = nl_langinfo (CODESET);],
   xmms_cv_func_langinfo=yes,
   xmms_cv_func_langinfo=no)])
if test "x$xmms_cv_func_langinfo" = "xyes"; then
  AC_DEFINE([HAVE_CODESET],,[Define if nl_langinfo(CODESET) is available.])
fi


AC_OUTPUT([
Makefile
])
